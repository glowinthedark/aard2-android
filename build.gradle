plugins {
    id 'com.android.application' version '8.7.3'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }


    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias System.getenv("KEY_ALIAS")
            }

            debug {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias System.getenv("KEY_ALIAS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-release.pro'
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    defaultConfig {
        namespace "itkach.aard2"
        applicationId "com.legbehindneck.aard2"
        minSdkVersion 21
        targetSdkVersion 34
        compileSdk 34
        applicationIdSuffix 'darkmod'
        versionName "13.0201.16"
        versionCode  64
        setProperty("archivesBaseName", "aard25-" + versionCode + "-" + applicationIdSuffix)
    }

    compileOptions {
//        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    compileSdk 34
    buildToolsVersion '34.0.0'

}

dependencies {
    // https://developer.android.com/studio/write/java8-support#library-desugaring
    // For AGP 7.4+
//    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.4")
    // For AGP 7.3
    // coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.2.3")
    // For AGP 4.0 to 7.2
    // coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.9")
    api 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    api 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    implementation 'androidx.webkit:webkit:1.12.1'
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.viewpager2:viewpager2:1.1.0"
    api project(':slobj')
    api project(':slobber')
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
