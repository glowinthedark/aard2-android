plugins {
    id 'com.android.application' version '8.7.3'
    id 'org.jetbrains.kotlin.android' version '1.9.25'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    namespace "itkach.aard2"

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    packagingOptions {
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias System.getenv("KEY_ALIAS")
            }

            debug {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias System.getenv("KEY_ALIAS")
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        compileSdk 35
        versionName "1.0.3"
        versionCode  61
        applicationId "com.legbehindneck.aard2"
        applicationIdSuffix 'darkmod'
        setProperty("archivesBaseName", "aard2vark-" + versionCode + "-" + applicationIdSuffix)
    }
    lint {
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.webkit:webkit:1.12.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.viewpager2:viewpager2:1.1.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.8.5"
    implementation "androidx.navigation:navigation-fragment-ktx:2.8.5"
    implementation "androidx.navigation:navigation-runtime-ktx:2.8.5"
    api 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    api 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    api project(':slobj')
    api project(':slobber')
    implementation 'androidx.core:core-ktx:1.15.0'
}
